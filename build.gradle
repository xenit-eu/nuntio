plugins {
    id 'org.ajoberstar.reckon' version '0.13.0'
    id "org.sonarqube" version "3.3"
    id "be.vbgn.dev-conventions" version "0.5.3"
}

reckon {
    scopeFromProp()
    snapshotFromProp()
}

allprojects {
    group 'be.xenit.nuntio'

    apply plugin: "be.vbgn.dev-conventions"

    repositories {
        mavenCentral()
    }


    plugins.withType(JavaBasePlugin.class) {
        project.apply plugin: "jacoco"

        java {
            sourceCompatibility = JavaVersion.VERSION_11
        }

        tasks.withType(Test.class).configureEach {
            useJUnitPlatform()
        }
    }

    plugins.withType(JavaPlugin.class) {
        dependencies {
            annotationProcessor 'org.projectlombok:lombok:1.18.20'
            compileOnly 'org.projectlombok:lombok:1.18.20'

            testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'
            testCompileOnly 'org.projectlombok:lombok:1.18.20'

            implementation platform(project(':nuntio-bom'))
            implementation 'org.slf4j:slf4j-api'

            testImplementation platform('org.junit:junit-bom:5.8.1')
            testImplementation 'org.junit.jupiter:junit-jupiter'
        }

    }

    plugins.withType(JavaLibraryPlugin.class) {
        dependencies {
            api platform(project(':nuntio-bom'))
        }
    }

}
